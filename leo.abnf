; Copyright (C) 2019-2023 Aleo Systems Inc.
; This file is part of the Aleo library.

; The Leo library is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.

; The Leo library is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with the Leo library. If not, see <https://www.gnu.org/licenses/>.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; This file specifies the grammar of Leo in the ABNF notation,
; which is defined in:
; - RFC 5234 (https://www.rfc-editor.org/rfc/rfc5234)
; - RFC 7405 (https://www.rfc-editor.org/rfc/rfc7405)

; Leo files are written in Unicode, encoded in UTF-8.
; The grammar in this file applies to the Unicode scalar values
; that result from the UTF-8 decoding of the files.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Lexical Grammar
; ---------------

horizontal-tab = %x9   ; <HT>

line-feed = %xA   ; <LF>

carriage-return = %xD   ; <CR>

space = %x20   ; <SP>

visible-ascii = %x21-7E

safe-ascii = horizontal-tab
           / line-feed
           / carriage-return
           / space
           / visible-ascii
           ; excludes control characters 0-8, 11, 12, 14-31, 127

safe-nonascii = %x80-2029 / %x202F-2065 / %x206A-D7FF / %xE000-10FFFF
                ; excludes bidi embeddings/overrides/isolates
                ; (i.e. %x202A-202E and %x2066-2069)
                ; and excludes high/low surrogates
                ; (i.e. %xD800-DFFF )

character = safe-ascii / safe-nonascii

line-terminator = line-feed / carriage-return / carriage-return line-feed

whitespace = space / horizontal-tab / line-terminator

comment = block-comment / line-comment

block-comment = "/*" rest-of-block-comment

rest-of-block-comment =
      "*" rest-of-block-comment-after-star
    / not-star-or-line-feed-or-carriage-return rest-of-block-comment
    / line-terminator rest-of-block-comment

rest-of-block-comment-after-star =
      "/"
    / "*" rest-of-block-comment-after-star
    / not-star-or-slash-or-line-feed-or-carriage-return rest-of-block-comment
    / line-terminator rest-of-block-comment

not-star-or-line-feed-or-carriage-return =
      horizontal-tab
    / %x20-29
    / %x2B-7E
    / safe-nonascii
    ; anything but * (%x2A) and <LF> and <CR>

not-star-or-slash-or-line-feed-or-carriage-return =
      horizontal-tab
    / %x20-29
    / %x2B-2E
    / %x30-7E
    / safe-nonascii
    ; anything but * (%x2A) and / (%x2F) and <LF> and <CR>

line-comment = "//" *not-line-feed-or-carriage-return

not-line-feed-or-carriage-return = horizontal-tab
                                 / space
                                 / visible-ascii
                                 / safe-nonascii
                                 ; anything but <LF> and <CR>

keyword = %s"address"
        / %s"assert"
        / %s"assert_eq"
        / %s"assert_neq"
        / %s"block"
        / %s"bool"
        / %s"console"
        / %s"constant"
        / %s"else"
        / %s"field"
        / %s"finalize"
        / %s"for"
        / %s"function"
        / %s"group"
        / %s"i8"
        / %s"i16"
        / %s"i32"
        / %s"i64"
        / %s"i128"
        / %s"if"
        / %s"import"
        / %s"in"
        / %s"inline"
        / %s"let"
        / %s"mapping"
        / %s"private"
        / %s"program"
        / %s"public"
        / %s"record"
        / %s"return"
        / %s"scalar"
        / %s"self"
        / %s"struct"
        / %s"then"
        / %s"transition"
        / %s"u8"
        / %s"u16"
        / %s"u32"
        / %s"u64"
        / %s"u128"

uppercase-letter = %x41-5A   ; A-Z

lowercase-letter = %x61-7A   ; a-z

letter = uppercase-letter / lowercase-letter

decimal-digit = %x30-39   ; 0-9

nonzero-decimal-digit = %x31-39   ; 1-9

identifier = letter *( letter / decimal-digit / "_" )
             ; but not a keyword or a boolean literal or aleo1...

numeral = 1*( decimal-digit *"_" )

tuple-index = "0" / nonzero-decimal-digit *( decimal-digit )

unsigned-literal = numeral ( %s"u8" / %s"u16" / %s"u32" / %s"u64" / %s"u128" )

signed-literal = numeral ( %s"i8" / %s"i16" / %s"i32" / %s"i64" / %s"i128" )

field-literal = numeral %s"field"

product-group-literal = numeral %s"group"

scalar-literal = numeral %s"scalar"

boolean-literal = %s"true" / %s"false"

address-literal = %s"aleo1" 58( lowercase-letter / decimal-digit )

integer-literal = unsigned-literal
                / signed-literal

numeric-literal = integer-literal
                / field-literal
                / product-group-literal
                / scalar-literal

atomic-literal = numeric-literal
               / boolean-literal
               / address-literal

annotation = "@" identifier

symbol = "!"
       / "&&" / "||"
       / "==" / "!="
       / "<" / "<=" / ">" / ">="
       / "&" / "|" / "^"
       / "<<" / ">>"
       / "+" / "-" / "*" / "/" / "%" / "**"
       / "="
       / "+=" / "-=" / "*=" / "/=" / "%=" / "**="
       / "<<=" / ">>="
       / "&=" / "|=" / "^="
       / "&&=" / "||="
       / "(" / ")"
       / "[" / "]"
       / "{" / "}"
       / "," / "." / ".." / ";" / ":" / "::" / "?"
       / "->" / "=>" / "_"
       / %s")group"

token = keyword
      / identifier
      / atomic-literal
      / numeral
      / annotation
      / symbol

lexeme = token / comment / whitespace

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Syntactic Grammar
; -----------------

program-id = identifier "." identifier

locator = program-id "/" identifier

unsigned-type = %s"u8" / %s"u16" / %s"u32" / %s"u64" / %s"u128"

signed-type = %s"i8" / %s"i16" / %s"i32" / %s"i64" / %s"i128"

integer-type = unsigned-type / signed-type

field-type = %s"field"

group-type = %s"group"

scalar-type = %s"scalar"

arithmetic-type = integer-type / field-type / group-type / scalar-type

boolean-type = %s"bool"

address-type = %s"address"

named-primitive-type =
    boolean-type / arithmetic-type / address-type

unit-type = "(" ")"

primitive-type = named-primitive-type / unit-type

named-type = named-primitive-type
           / identifier [ "." %s"record" ]
           / locator [ "." %s"record" ]

tuple-type = "(" type 1*( "," type ) [ "," ] ")"

type = named-type / unit-type / tuple-type

group-coordinate = ( [ "-" ] numeral ) / "+" / "-" / "_"

affine-group-literal = "(" group-coordinate "," group-coordinate %s")group"

literal = atomic-literal / affine-group-literal

group-literal = product-group-literal / affine-group-literal

primary-expression = literal
                   / variable
                   / associated-constant
                   / "(" expression ")"
                   / free-function-call
                   / associated-function-call
                   / unit-expression
                   / tuple-expression
                   / struct-expression
                   / self-caller
                   / block-height

variable = identifier

associated-constant = named-type "::" identifier

free-function-call = identifier function-arguments
                   / locator function-arguments

associated-function-call = named-type "::" identifier function-arguments

function-arguments = "(" [ expression *( "," expression ) [ "," ] ] ")"

unit-expression = "(" ")"

tuple-expression = "(" expression 1*( "," expression ) [ "," ] ")"

struct-expression = identifier "{" struct-component-initializer
                                   *( "," struct-component-initializer )
                                   [ "," ] "}"

struct-component-initializer = identifier
                             / identifier ":" expression

self-caller = %s"self" "." %s"caller"

block-height = %s"block" "." %s"height"

postfix-expression = primary-expression
                   / tuple-component-expression
                   / struct-component-expression
                   / method-call

tuple-component-expression = postfix-expression "." tuple-index

struct-component-expression = postfix-expression "." identifier

method-call = postfix-expression "." identifier function-arguments

unary-expression = postfix-expression
                 / "!" unary-expression
                 / "-" unary-expression

exponential-expression = unary-expression
                       / unary-expression "**" exponential-expression

multiplicative-expression = exponential-expression
                          / multiplicative-expression "*" exponential-expression
                          / multiplicative-expression "/" exponential-expression
                          / multiplicative-expression "%" exponential-expression

additive-expression = multiplicative-expression
                    / additive-expression "+" multiplicative-expression
                    / additive-expression "-" multiplicative-expression

shift-expression = additive-expression
                 / shift-expression "<<" additive-expression
                 / shift-expression ">>" additive-expression

conjunctive-expression = shift-expression
                       / conjunctive-expression "&" shift-expression

disjunctive-expression = conjunctive-expression
                       / disjunctive-expression "|" conjunctive-expression

exclusive-disjunctive-expression =
      disjunctive-expression
    / exclusive-disjunctive-expression "^" disjunctive-expression

ordering-expression =
      exclusive-disjunctive-expression
    / exclusive-disjunctive-expression "<" exclusive-disjunctive-expression
    / exclusive-disjunctive-expression ">" exclusive-disjunctive-expression
    / exclusive-disjunctive-expression "<=" exclusive-disjunctive-expression
    / exclusive-disjunctive-expression ">=" exclusive-disjunctive-expression

equality-expression = ordering-expression
                    / ordering-expression "==" ordering-expression
                    / ordering-expression "!=" ordering-expression

conditional-conjunctive-expression =
      equality-expression
    / conditional-conjunctive-expression "&&" equality-expression

conditional-disjunctive-expression =
      conditional-conjunctive-expression
    / conditional-disjunctive-expression "||" conditional-conjunctive-expression

binary-expression = conditional-disjunctive-expression

conditional-ternary-expression = binary-expression
                               / binary-expression "?" expression ":" expression

expression = conditional-ternary-expression

statement = return-statement
          / expression-statement
          / variable-declaration
          / const-declaration
          / conditional-statement
          / loop-statement
          / assignment-statement
          / console-statement
          / block

block = "{" *statement "}"

return-statement =
    %s"return" [ expression ] [ %s"then" %s"finalize" function-arguments ] ";"

expression-statement = expression ";"

variable-declaration = %s"let" identifier-or-identifiers ":" type
                       "=" expression ";"

const-declaration = %s"const" identifier ":" type "=" expression ";"

identifier-or-identifiers = identifier
                          / "(" identifier 1*( "," identifier ) [ "," ] ")"

branch = %s"if" expression block

conditional-statement = branch
                      / branch %s"else" block
                      / branch %s"else" conditional-statement

loop-statement = %s"for" identifier ":" type
                 %s"in" expression ".." [ "=" ] expression
                 block

assignment-operator = "="
                    / "+="
                    / "-="
                    / "*="
                    / "/="
                    / "%="
                    / "**="
                    / "<<="
                    / ">>="
                    / "&="
                    / "|="
                    / "^="
                    / "&&="
                    / "||="

assignment-statement = expression assignment-operator expression ";"

console-statement = %s"console" "." console-call ";"

console-call = assert-call
             / assert-equal-call
             / assert-not-equal-call

assert-call = %s"assert" "(" expression ")"

assert-equal-call = %s"assert_eq" "(" expression "," expression [ "," ] ")"

assert-not-equal-call = %s"assert_neq" "(" expression "," expression [ "," ] ")"

function-declaration = *annotation %s"function" identifier
                       "(" [ function-parameters ] ")"
                       [ "->" type ]
                       block

function-parameters = function-parameter *( "," function-parameter ) [ "," ]

function-parameter = [ %s"public" / %s"private" / %s"constant" ]
                     identifier ":" type

inline-declaration = *annotation %s"inline" identifier
                     "(" [ function-parameters ] ")"
                     [ "->" type ]
                     block

transition-declaration =
    *annotation %s"transition" identifier
    "(" [ function-parameters ] ")"
    [ "->" [ %s"public" / %s"private" / %s"constant" ] type ]
    block [ finalizer ]

finalizer =
    %s"finalize" identifier
    "(" [ function-parameters ] ")"
    [ "->" [ %s"public" / %s"private" / %s"constant" ] type ]
    block

struct-declaration = %s"struct" identifier
                     "{" struct-component-declarations "}"

struct-component-declarations = struct-component-declaration
                                *( "," struct-component-declaration )
                                [ "," ]

struct-component-declaration =
    [ %s"public" / %s"private" / %s"constant" ] identifier ":" type

record-declaration = %s"record" identifier
                     "{" struct-component-declarations "}"

mapping-declaration = %s"mapping" identifier ":" type "=>" type ";"

program-item = function-declaration
             / inline-declaration
             / transition-declaration
             / struct-declaration
             / record-declaration
             / mapping-declaration
             / const-declaration

program-declaration = %s"program" program-id "{" *program-item "}"

import-declaration = %s"import" program-id ";"

file = *import-declaration program-declaration

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Input Grammar
; -------------

input-type = type

input-expression = expression

input-item = identifier ":" input-type "=" input-expression ";"

input-title = "[" ( %s"public" / %s"private" / %s"constant" ) "]"

input-section = input-title *input-item

input-file = *input-section

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Output Grammar
; --------------

output-expression = expression

output-item = output-expression ";"

output-title = "[" %s"output" "]"

output-section = output-title output-item

output-file = output-section
